import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "xyz.jpenilla.run-paper" version "1.0.6"
}

allprojects {
    apply plugin: 'java-library'

    group = pluginGroup
    version = pluginVersion

    // Target Java 8 by default for legacy support
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)

    repositories {
        mavenCentral()
        maven {
            // Spigot
            url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            // VaultAPI
            url = 'https://jitpack.io'
        }
        maven {
            // WorldEdit
            url = 'https://maven.enginehub.org/repo/'
        }
        maven {
            // ProtocolLib
            url "https://repo.dmulloy2.net/repository/public/"
        }
        // PaperLib
        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }
        // AnvilGUI
        maven {
            url 'https://repo.codemc.io/repository/maven-snapshots/'
        }
        flatDir {
            dirs "$rootDir/libs"
        }
    }

    processResources {
        filesMatching("plugin.yml") {
            filter ReplaceTokens, tokens: [version: version]
        }
    }
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    //shadow 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.14.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'
    implementation "io.papermc:paperlib:1.0.5"
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    implementation project('Support-WorldEdit-6')
    implementation project('Support-WorldEdit-7')
    implementation project(":adapters")
    runtimeOnly project(path: ":adapters:1_18_R1", configuration: "reobf")
    runtimeOnly project(path: ":adapters:1_18_R2", configuration: "reobf")
}

shadowJar {
    classifier = ''
    relocate 'io.papermc.lib', 'com.gmail.val59000mc.paperlib'
    relocate 'net.wesjd.anvilgui', 'com.gmail.val59000mc.anvilgui'
    relocate 'com.pieterdebot.biomemapping', 'com.gmail.val59000mc.biomemapping'
}

build.dependsOn(shadowJar)

runServer {
    minecraftVersion "1.18.2"
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
